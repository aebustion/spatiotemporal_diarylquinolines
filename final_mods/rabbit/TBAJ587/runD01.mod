;; 1. Based on: runG50
;; 2. Description: G50 + inverse model with all caseum samples (no hockey stick), new S587 plasma model
;; x1. Author: aebustion
$PROBLEM    rabbit PK
$INPUT      ID RABBITID OLDID=DROP DOSEX DOSEFLAT NDOSES TIME TAD NGML
            NDV EVID MDV AMT II ADDL LESION METAB INFECT WT METHOD
            BIOPSY CMT TUBENO DIST INOUTRANK BLQ iF1 LESIONBIN
            SURFACEAREATOTAL SURFACEAREAUM2 DV
$DATA      TBAJ587_plasma_lesion_data_nm_restructure_logDV.csv
            IGNORE=@ IGNORE(LESION.EQ.0) IGNORE(LESION.EQ.1)
            IGNORE(LESION.EQ.2) IGNORE(LESION.EQ.3)
            IGNORE(TUBENO.EQ.-99) IGNORE(BLQ.EQ.1)

$SUBROUTINE ADVAN6 TOL=9
$MODEL      NCOMP=8 
            COMP=(ABS,DEFDOSE) 
            COMP=(CENTRAL,DEFOBS)
            COMP=(PERIPH) 
            COMP=(TRANSIT) 
            COMP=(METAB)
            COMP=(METABPERIPH) 
            COMP=(CASEUMPARENT) 
            COMP=(CASEUMMETAB)
$PK

IF(METAB.EQ.0) L2 = 1
IF(METAB.EQ.1) L2 = 2

CL = THETA(3)
KM = THETA(6)

V  = THETA(4)

KA = THETA(5)
S2 = V
F1 = 1 * EXP(ETA(1))

ALAG1 = THETA(7)

Q = THETA(8)
V2 = THETA(9)

CLM = THETA(10)
VM = THETA(11)

QM = THETA(12)
VM2 = THETA(13)

IF(DIST.LE.10) THEN
DISTN = 10
ELSE
DISTN = DIST
ENDIF

DISTCOV = THETA(16)
DISTCOVGAMMA = THETA(20)
TVKPL7 = THETA(14) 
KPL7 = TVKPL7

TVPC7 = exp(THETA(15))/DISTN**DISTCOVGAMMA
PC7 = TVPC7

DISTCOVMETAB = THETA(19)
DISTCOVMETABGAMMA = THETA(21)
TVKPL8 = THETA(17) 
KPL8 = TVKPL8

TVPC8 = exp(THETA(18))/DISTN**DISTCOVMETABGAMMA
PC8 = TVPC8

$DES      
DADT(1)=-KA*A(1)
DADT(2)= KA*A(4)- CL/V*A(2) - Q/V*A(2) + Q/V2*A(3) 
DADT(3)= Q/V*A(2) - Q/V2*A(3)
DADT(4)= KA*A(1)-KA*A(4)

DADT(5)= CL/V*A(2)-CLM/VM*A(5) - QM/VM*A(5) + QM/VM2*A(6) 
DADT(6)= QM/VM*A(5) - QM/VM2*A(6)

DADT(7)= KPL7 * (PC7 * A(2)/V - A(7))
DADT(8)= KPL8 * (PC8 * A(5)/VM - A(8))

$ERROR         

CP = A(2)/V
CM = A(5)/VM

DEL = 1E-12
IPRED = LOG(A(7)+DEL)
IF(L2.EQ.2) IPRED = LOG(A(8)+DEL)

W = SQRT( (THETA(1)/EXP(IPRED))**2 + THETA(2))
IF(L2.EQ.2) W = SQRT( (THETA(6)/EXP(IPRED))**2 + THETA(7))
IF(W.EQ.0) W = 1


IRES = DV-IPRED
IWRES = IRES/W


Y = IPRED+W*EPS(1)
IF(L2.EQ.2) Y = IPRED+W*EPS(2)

$THETA  (0,0.421528) ; 1 prop Error
 (0,0.221896) ; 2 add Error
 (0,7.12) FIX ; 3 CL L/h
 (0,89.4) FIX ; 4 V L
 (0,0.283) FIX ; 5 KA 1/h
 (0,0.238709) ; 6 metab prop Error
 (0,0.631876) ; 7 metab add Error
 (0,13.5) FIX ; 8 Q
 (0,182) FIX ; 9 V2
 (0,12.8) FIX ; 10 CLM
 (0,38.9) FIX ; 11 VM
 (0,41.3) FIX ; 12 QM
 (0,353) FIX ; 13 VM2
 (0,0.00891594) ; 14 KPL7
 (0,12.2669) ; 15 PC7
 0 FIX ; 16 DISTCOV
 (0,0.0167351) ; 17 KPL8
 (0,12.8948) ; 18 PC8
 0 FIX ; 19 DISTCOV
 (0,1.86104) ; 20 DISTCOVGAMMA
 (0,1.89186) ; 21 DISTCOVMETABGAMMA
$OMEGA  
 0.40602384  FIX  ;    1 F IIV
$SIGMA  
 1  FIX  ;    1 error
 1  FIX  ;    2 error
$ESTIMATION METHOD=1 INTERACTION PRINT=5 NSIG=3 SIGL=9 MAXEVAL=9999
            NOABORT
$COVARIANCE UNCONDITIONAL
$TABLE      ID RABBITID BIOPSY TIME TAD IPRED PRED DV METAB LESION
            SURFACEAREATOTAL SURFACEAREAUM2 AMT DOSEX DIST WRES IWRES
            CWRES EVID F1 CL V KPL7 PC7 KPL8 PC8 INFECT WT
            FILE=sdtabD01 NOPRINT ONEHEADER

