;; 1. Based on: run60
;; 2. Description: 59 + add back metab periph
;; x1. Author: user
$PROBLEM    rabbit PK
$INPUT      ID OLDID=DROP DOSEX DOSEFLAT NDOSES TIME TAD NGML DV EVID MDV AMT II ADDL 
			LESION METAB INFECT WT METHOD BIOPSY CMTX TUBENO LSIZE TISSUEWT DILUTION PBS DIST INRANK BLQ
$DATA       ./TBAJ587_plasma_lesion_data_nm_longterm_rabbit_only.csv 
IGNORE=@ 
IGNORE(LESION.GE.0)

$SUBROUTINE ADVAN6 TOL=5
$MODEL      NCOMP=6 COMP=(ABS,DEFDOSE) COMP=(CENTRAL, DEFOBS) COMP=(PERIPH) COMP=(TRANSIT) COMP=(METAB) COMP=(METABPERIPH)
$PK

IF(METAB.EQ.0) L2 = 1
IF(METAB.EQ.1) L2 = 2

IF(L2.EQ.1) CMT = 2
IF(L2.EQ.2) CMT = 5
       
CL = THETA(3)
KM = THETA(6)

V  = THETA(4) * EXP(ETA(2))

KA = THETA(5)
S2 = V
F1 = 1 * EXP(ETA(3))

ALAG1 = THETA(7)

Q = THETA(8)
V2 = THETA(9)

CLM = THETA(10)
VM = THETA(11)

QM = THETA(12)
VM2 = THETA(13)

$DES      
DADT(1)=-KA*A(1)
DADT(2)= KA*A(4)- CL/V*A(2) - Q/V*A(2) + Q/V2*A(3)
DADT(3)= Q/V*A(2) - Q/V2*A(3)
DADT(4)= KA*A(1)-KA*A(4)

DADT(5)= CL/V*A(2)-CLM/VM*A(5) - QM/VM*A(5) + QM/VM2*A(6)
DADT(6)= QM/VM*A(5) - QM/VM2*A(6)

$ERROR         

CP = A(2)/V
CM = A(5)/VM

IPRED = A(2)/V
IF(METAB.EQ.1) IPRED = A(5)/VM

IF(METAB.EQ.0) W = IPRED
IF(METAB.EQ.1) W = IPRED

IF(W.EQ.0) W = 1
IRES = DV-IPRED
IWRES = IRES/W

Y = IPRED + W*EPS(1)
IF(METAB.EQ.1) Y = IPRED + W*EPS(2)

$THETA
(0) FIX ; 1 Parent Error
(0) FIX ; 2 Metab Error
(0, 5.54) ; 3 CL L/h
(0, 50.3) ; 4 V L
(0, 0.238) ; 5 KA 1/h
(0) FIX ; 6 KM
(0) FIX ; 7 ALAG1
(0, 27) ; 8 Q
(0, 386) ; 9 V2
(0, 12.3) ; 10 CLM
(0, 70.5) ; 11 VM
(0, 25) ; 12 QM
(0, 250) ; 13 VM2

$OMEGA 
 0 FIX  ; 1 Additive Error
 0 FIX  ;2 IIV V
 0.402 ; 3 IIV F1

$SIGMA
 0.256 ; 1 Parent prop error 
 0.176 ; 2 Metab prop error 

$ESTIMATION METHOD=1 INTERACTION PRINT=5 SIG=3 MAXEVAL=9999 NOABORT
$COVARIANCE UNCONDITIONAL 

$TABLE ID TIME TAD IPRED PRED DV AMT DOSEX WRES IWRES CWRES EVID F1 CL V INFECT WT METAB FILE=sdtab61 NOPRINT ONEHEADER
$TABLE ID CL V KA Q V2 WT ETA(2) FILE=patab61 NOPRINT ONEHEADER
